import com.wix.mysql.config.*;
import com.wix.mysql.distribution.*
import java.util.concurrent.*
import com.wix.mysql.*

buildscript {
    dependencies {
        classpath 'mysql:mysql-connector-java:' + mysqlVersion
        classpath 'org.flywaydb:flyway-gradle-plugin:' + flywayVersion
        classpath 'com.angkorteam.gradle:plugintask:' + plugintaskVersion
        classpath 'com.wix:wix-embedded-mysql:' + wixVersion
    }
    repositories {
        maven { url "http://dl.bintray.com/socheat/maven" }
        jcenter();
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'com.angkorteam.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'mbaas-jooq'
version = '1.0'

dependencies {
    compile 'org.jooq:jooq:' + jooqVersion
    compile 'joda-time:joda-time:' + jodatimeVersion
    compile 'com.angkorteam.gradle:plugintask:' + plugintaskVersion
    compile 'mysql:mysql-connector-java:' + mysqlVersion
    compile 'org.apache.commons:commons-dbcp2:' + commonsdbcp2Version
    compile 'org.apache.commons:commons-lang3:' + commonslang3Version
    compile 'commons-io:commons-io:' + commonsioVersion
    compile 'com.wix:wix-embedded-mysql:' + wixVersion
}

sourceSets {
    main {
        java {
            srcDirs 'src/java', 'buildSrc/src/main/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

flyway {
    // driver = project.ext.get('temp.jdbc.driver')
    driver = 'com.mysql.jdbc.Driver'
    // url = 'jdbc:mysql://' + project.ext.get('temp.jdbc.hostname') + ':' + project.ext.get('temp.jdbc.port') + '/' + project.ext.get('temp.jdbc.database') + '?' + project.ext.get('temp.jdbc.extra')
    url = 'jdbc:mysql://localhost:9306/mbaas_temp?createDatabaseIfNotExist=true&autoReconnect=true&useSSL=false&serverTimezone=UTC'
    // project.ext.get('temp.jdbc.username')
    user = 'mbaas_temp'
    // project.ext.get('temp.jdbc.password')
    password = 'secret'
    encoding = 'UTF-8'
    baselineOnMigrate = true
}

jooq {
    // jdbcDriver = project.ext.get('temp.jdbc.driver')
    jdbcDriver = 'com.mysql.jdbc.Driver'
    // jdbcUrl = 'jdbc:mysql://' + project.ext.get('temp.jdbc.hostname') + ':' + project.ext.get('temp.jdbc.port') + '/' + project.ext.get('temp.jdbc.database') + '?' + project.ext.get('temp.jdbc.extra')
    jdbcUrl = 'jdbc:mysql://localhost:9306/mbaas_temp?createDatabaseIfNotExist=true&autoReconnect=true&useSSL=false&serverTimezone=UTC'
    // jdbcUser = project.ext.get('temp.jdbc.username')
    jdbcUser = 'mbaas_temp';
    // jdbcPassword = project.ext.get('temp.jdbc.password')
    jdbcPassword = 'secret'
    database = 'mbaas_temp'
    dialect = 'org.jooq.util.mysql.MySQLDatabase'
    packageName = 'com.angkorteam.mbaas.model.entity'
    outputDirectory = 'buildSrc/src/main/java'
}

task startMySQL {
    doLast {
        def config = MysqldConfig.aMysqldConfig(Version.v5_6_latest)
                .withCharset(Charset.UTF8)
                .withPort(9306)
                .withUser("mbaas_temp", "secret")
                .withTimeZone("Asian/Phnom_Penh")
                .withTimeout(2, TimeUnit.MINUTES)
                .build();

        def mysqld = EmbeddedMysql.anEmbeddedMysql(config)
                .addSchema("mbaas_temp")
                .start();
    }
}

flywayClean.dependsOn startMySQL
